plugins {
	id 'com.diffplug.spotless' version '7.0.4'
}

repositories {
	mavenCentral()
}

allprojects {
	group = 'frc.team581'
	version = '2025.0.0'
}

subprojects {
	apply plugin: 'java'

	java {
		sourceCompatibility = JavaVersion.VERSION_17
		targetCompatibility = JavaVersion.VERSION_17
	}

	repositories {
		mavenCentral()
	}

	// Configure string concat to always inline compile
	tasks.withType(JavaCompile) {
		options.compilerArgs.add '-XDstringConcat=inline'
	}

	// Common test configuration
	test {
		useJUnitPlatform()
		systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
	}
}

// Spotless configuration applied to all projects

spotless {
	enforceCheck = false

	encoding = 'UTF-8'
	lineEndings = 'UNIX'

	java {
		target 'shared/**/*.java', 'comp-bot/**/*.java', 'offseason-bot/**/*.java', 'new-member-bot/**/*.java'
		googleJavaFormat('1.26.0')
		licenseHeader ''

		targetExclude('**/generated/**')
	}

	groovyGradle {
		target '**/*.gradle'
		greclipse()
		leadingSpacesToTabs()
		endWithNewline()
	}

	json {
		target '**/*.json'
		simple().indentWithSpaces(2)

		targetExclude('.pathplanner/**')
	}
}

// Prevent root deploy to avoid dangerous parallel deployments
task deploy {
	doLast {
		throw new GradleException("""
‚ùå Running the deploy task without specifying a robot is disabled to prevent dangerous parallel deployments!

Instead, deploy to a specific robot:
./gradlew comp-bot:deploy       - Deploy to competition robot
./gradlew offseason-bot:deploy  - Deploy to offseason robot
./gradlew new-member-bot:deploy - Deploy to new member robot

This prevents race conditions where each robot tries to deploy at the same time, causing the wrong code to be deployed.
		""".trim())
	}
}
